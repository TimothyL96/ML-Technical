plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.minimal.application") version "3.6.0"
    id 'org.liquibase.gradle' version '2.1.1'
}

version = "0.1"
group = "com.ml"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("io.micronaut.data:micronaut-data-processor")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")

    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut.data:micronaut-data-hibernate-jpa")
    implementation("io.micronaut.liquibase:micronaut-liquibase")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation("io.swagger.core.v3:swagger-annotations")
    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation("ch.qos.logback:logback-classic")
    implementation("io.micronaut:micronaut-validation")

    // Test
    testImplementation("org.mockito:mockito-core")

    // DB Connectors
    runtimeOnly("mysql:mysql-connector-java")
    implementation("com.h2database:h2")

    // Liquibase
    liquibaseRuntime 'org.liquibase:liquibase-core:4.16.1'
    liquibaseRuntime 'mysql:mysql-connector-java:8.0.30'
    liquibaseRuntime 'info.picocli:picocli:4.6.3'
    implementation("org.slf4j:jul-to-slf4j:1.7.30")
}


application {
    mainClass.set("com.ml.Application")
}
java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.ml.*")
    }
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute(module("io.micronaut:micronaut-jackson-databind"))
                .using(module("io.micronaut.serde:micronaut-serde-jackson:1.3.2"))
    }
}

String migrationFileName = new Date().format("yyyyMMddHHmmss") + "-" + (project.ext.properties.desc ?: 'changelog') + ".h2.sql"

liquibase {
    activities {
        diff {
            changeLogFile "src/main/resources/migrations/changelogs/${migrationFileName}"
            url project.ext.properties.datasourceBaseUrl
            username project.ext.properties.datasourceBaseUsername
            password project.ext.properties.datasourceBasePassword

            referenceUrl project.ext.properties.datasourceMainUrl
            referenceUsername project.ext.properties.datasourceMainUsername
            referencePassword project.ext.properties.datasourceMainPassword
        }
    }
}
